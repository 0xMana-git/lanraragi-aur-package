#Maintainer: SadaoMaou <luxrayguy@gmail.com>
#Contributor: Satiricon <david.satiricon@gmail.com>
#Contributor: Alexbrenvero <alexandre@lempire.xyz>
pkgname=lanraragi
pkgver=v.0.9.21
pkgrel=1
pkgdesc="LANraragi build package"
arch=(any)
url="https://github.com/Difegue/LANraragi"
license=("MIT")
depends=(libarchive ghostscript perl gnupg redis imagemagick libwebp openssl zlib perl-local-lib perl-uri)
makedepends=(cpanminus npm perl-config-autoconf pkgconf)
source=("https://github.com/Difegue/LANraragi/archive/${pkgver}.tar.gz"
        "lanraragi.service" "lanraragi.sysusers" "lanraragi.tmpfiles" "_des_patched.h")
sha512sums=(
"bbf7397ad621bf13f6c074ad9b92259a3b38847abbb9ce1dd1913b0b26b9968b70471431d70967af61554998e814e2ca50b3281e1f8b65f364837099eed1200f"
"1ef2ca60e51269351440c1ae77431c46b8b82eb7d285e6d209ca9ac64141e2a337a5e9abca49425d026a10194b341c6c22966708bfa8f81d3904c8bc490123e6"
"c598b37c691b66c3c32aed50d0e79c9d75708c40f79fe83f287e6ed1592736f608b646427ef044bf489d034a91219fd1567b3d0ece320633176ae5dbe28b7685"
"0f66197d8fe253d1f6ff56f4e301c337be66aa82cf091619df1a8c17ec97d66721f2618cdcdf88718f28f0d80263d4331e482d22468e1bd7bd55ecb2bc076a2d"
"696dd577e0428319b7aacd66ce6dca27c6f3c6b6b9dcd086fa56ffdef293caa48fab1711ff3b4260a0050eefaa87d48a4f44ba4b7fbbb170c825e6088a99b8f2"
)


build() {
  PATH=/usr/bin/vendor_perl:$PATH
  
  cd LANraragi-${pkgver}
  export PERL_CPANM_OPT="--local-lib=./perl5 $PERL_CPANM_OPT"

  #installs the broken package manually
  #this package hasnt updated in years so we should be good
  cryptdes_package="Crypt-DES-2.07"
  cryptdes_archive="$cryptdes_package.tar.gz"
  curl https://cpan.metacpan.org/authors/id/D/DP/DPARIS/Crypt-DES-2.07.tar.gz --output $cryptdes_archive
  tar zxf $cryptdes_archive
  cd $cryptdes_package
  cp ../../_des_patched.h _des.h
  perl Makefile.PL INSTALL_BASE="../perl5"
  make
  make test
  make install
  cd ..

  #run installer
  npm run lanraragi-installer install-full
}


package() {
  install -d -m 755 "${pkgdir}/usr/lib/lanraragi/perl5"
  cp -dpr --no-preserve=ownership "${srcdir}/LANraragi-${pkgver}/"* "${pkgdir}/usr/lib/lanraragi"
  chmod -R a=,a+rX,u+w ${pkgdir}/usr/lib/lanraragi

  install -D -m 644 "${srcdir}/lanraragi.service" "${pkgdir}/usr/lib/systemd/system/lanraragi.service"
  install -D -m 644 "${srcdir}/lanraragi.sysusers" "${pkgdir}/usr/lib/sysusers.d/lanraragi.conf"
  install -D -m 644 "${srcdir}/lanraragi.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/lanraragi.conf"
  install -D -m 644 "${srcdir}/LANraragi-${pkgver}/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  grep -rnwl ${pkgdir} -e ${srcdir} | xargs -I % sed -i 's,'"$srcdir"',\.,g' %

}
